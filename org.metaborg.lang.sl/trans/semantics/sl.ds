module trans/semantics/sl

imports
  trans/semantics/functions
  trans/semantics/statements
  trans/semantics/expressions
  trans/semantics/objects
  trans/semantics/builtins
  trans/semantics/values

signature
  sorts
    ShellInit

  constructors
    ShellInit : ShellInit

  arrows
    Program -init-> V
    ShellInit -init-> ShellInit
    Program -shell-> V
    Shell -shell-> U
    FunDef -shell-> V
    Stmt -shell-> U
    Expr -shell-> V

rules // loading and init

  ProgramDesug(fdefs, e) -init-> v
  where
    fdefs :: E {}, C C({}) -load-> _ :: E', C C(E);
    e :: E', C C(E) --> v :: E _, C _.

  ShellInit() -init-> ShellInit() :: E { }, C C({ }).

  ProgramDesug(fdefs, e) :: E, C -shell-> v :: E', C'
  where
    fdefs :: E, C -load-> _ :: E1, C1;
    e :: E1, C1 --> v :: E', C'.

  StmtBlock(Block(ss)) :: E, C -shell-> u :: E', C'
  where
    ss :: E, C --> u :: E', C'.

  FunDef(name, params, body) :: E, C C(EC) -shell-> clos :: E', C'
  where
    ClosV(name, params, __Block2Stmt__(body), E) => clos;
	{ name |--> clos } => entry;
	{ entry, E } => E';
	C({ entry, EC }) => C'.

  s : Stmt :: E, C -shell-> u :: E', C'
  where
    s :: E, C --> u :: E', C'.

  e : Expr :: E, C -shell-> v :: E', C'
  where
    e :: E, C --> v :: E', C'.
